def withDockerNetwork(Closure inner) {
//https://www.kabisa.nl/tech/running-multiple-docker-containers-in-parallel-with-jenkins/
  try {
    networkId = UUID.randomUUID().toString()
    sh "docker network create ${networkId}"
    inner.call(networkId)
  } finally {
    sh "docker network rm ${networkId}"
  }
}

void setBuildStatus(String message, String state) {
  step([
      $class: "GitHubCommitStatusSetter",
      reposSource: [$class: "AnyDefinedRepositorySource"],
      contextSource: [$class: "DefaultCommitContextSource"],
      errorHandlers: [[$class: "ChangingBuildStatusErrorHandler", result: "UNSTABLE"]],
      statusResultSource: [ $class: "ConditionalStatusResultSource", results: [[$class: "AnyBuildResult", message: message, state: state]] ]
  ]);
}

pipeline {                                                                                                                                                                                 
    environment {                                                                                                                                                                          
        IMAGE = "toleksa/${JOB_NAME}"
        SUBDIR = "."
        WWW_SUBDIR = "www/"
        CREDENTIALS = credentials('dockerhub')
        DB_PORT=13306
        DB_DATABASE="python_rest_api"
        REDIS_PORT=16379
        API_PORT=15000
        API_URL="http://api:${API_PORT}"
        WWW_PORT=18000
        WWW_URL="http://www:${WWW_PORT}"
        PROMETHEUS_PORT=19090
        PROMETHEUS_URL="http://prometheus:${PROMETHEUS_PORT}"
    }                                                                                                                                                                                      
    agent{                                                                                                                                                                                 
        label ''                                                                                                                                                                           
    }                                                                                                                                                                                      
    stages{                                                                                                                                                                                
        stage('Build'){                                                                                                                                                                    
            steps{                                                                                                                                                                         
                script{                                                                                                                                                                    
                    docker_image = docker.build("${IMAGE}:${BUILD_NUMBER}","${SUBDIR}")
                    docker_image.tag('latest')
                    docker_image_www = docker.build("${IMAGE}-www:${BUILD_NUMBER}","${WWW_SUBDIR}")
                    docker_image_www.tag('latest')
                    docker_image_tests = docker.build("${IMAGE}-tests:${BUILD_NUMBER}","-f tests/Dockerfile .")
                    docker_image_tests.tag('latest')
                }
                sh 'docker images'
            }                                                                                                                                                                              
        }
        stage('Unit Test'){
          steps{
            script {
              tests_image = docker.image("${IMAGE}-tests:${BUILD_NUMBER}")
              tests_image.inside(){
                sh 'cd /pytest ; PYLINTHOME=${OLDPWD} pylint --output-format=parseable --fail-under=3 /pytest/app/app.py --msg-template="{path}:{line}: [{msg_id}({symbol}), {obj}] {msg}" | tee ${OLDPWD}/pylint.log || echo "pylint exited with $?"'
                sh 'cd /pytest ; pytest -o cache_dir=/tmp/.pytest_cache --junit-xml=$OLDPWD/test_unit_result.xml /pytest/test_unit.py'
              }
              recordIssues enabledForFailure: true, aggregatingResults: true, tool: pyLint(pattern: 'pylint.log')
            }
          }
          post {
            always {
              junit 'test_unit_result.xml'
            }
          }
        }
        stage('Integration Test') {
          steps {
            script {
              withDockerNetwork{ n ->
                docker.image("mariadb:latest").withRun("--network ${n} --network-alias db --hostname db -e MARIADB_PASSWORD='password' \
                    -e MARIADB_USER='user' -e MARIADB_DATABASE='python_rest_api' -e MARIADB_ROOT_PASSWORD=password \
                    --mount type=bind,source=${WORKSPACE}/app/init.sql,target=/docker-entrypoint-initdb.d/init.sql","--port ${DB_PORT}") { c ->
                  docker.image("redis:alpine").withRun("--network ${n} --network-alias redis --hostname redis","redis-server \
                      --port ${REDIS_PORT}") {
                    docker.image("${IMAGE}:${BUILD_NUMBER}").withRun("--network ${n} --network-alias api --hostname api \
                        -e DB_PASS=password -e DB_USER=user -e DB_HOST=db -e DB_PORT=${DB_PORT} -e DB_DATABASE=${DB_DATABASE} -e REDIS_HOST=redis \
                        -e REDIS_PORT=${REDIS_PORT} -e API_PORT=${API_PORT}") { 
                      docker.image("${IMAGE}-www:${BUILD_NUMBER}").withRun("--network ${n} --network-alias www --hostname www \
                          -e WWW_PORT=${WWW_PORT} -e ENV_API_URL=${API_URL}") {
                        sh "sed -e 's@api:5000@api:'${API_PORT}'@g' prometheus/prometheus.yml |\
                            sed -e 's@scrape_interval: 15s@scrape_interval: 1s@g' > prometheus/jenkins.yml"
                        docker.image("bitnami/prometheus:latest").withRun("--network ${n} \
                            --network-alias prometheus --hostname prometheus -e API_URL=${API_URL} \
                            -v ${WORKSPACE}/prometheus/jenkins.yml:/opt/bitnami/prometheus/conf/prometheus.yml:Z","\
                              --config.file=/opt/bitnami/prometheus/conf/prometheus.yml \
                              --web.listen-address='0.0.0.0:${PROMETHEUS_PORT}' \
                              --storage.tsdb.path=/opt/bitnami/prometheus/data \
                              --web.console.libraries=/opt/bitnami/prometheus/conf/console_libraries \
                              --web.console.templates=/opt/bitnami/prometheus/conf/consoles") {
                          pytest_integration_image = docker.build("${IMAGE}-pytest-integration:${BUILD_NUMBER}","-f tests/integration/Dockerfile .")
                          pytest_integration_image.tag("latest")
                          pytest_integration_image.inside("--network ${n} --network-alias pytest_integration --hostname pytest_integration \
                              -e API_URL=${API_URL} -e PROMETHEUS_URL=${PROMETHEUS_URL} -e WWW_URL=${WWW_URL} \
                              -v ${WORKSPACE}/tests/integration:/pytest") {
                            sh 'counter=1 ; until $(curl --output /dev/null --silent --head --fail $API_URL/health); do if [ "$counter" -gt 30 ]; then \
                                echo "ERR: python-rest-api app not ready, exiting" ; exit 1 ; fi ; counter=$((counter+1)) ; printf "." ; sleep 1 ; done ; \
                                pytest -o cache_dir=/tmp/.pytest_cache --junit-xml=test_integration_result.xml /pytest/test_integration.py ;\
                                pytest -o cache_dir=/tmp/.pytest_cache --junit-xml=test_www_result.xml /pytest/test_www.py ;\
                                pytest -o cache_dir=/tmp/.pytest_cache --junit-xml=test_prometheus_result.xml /pytest/test_prometheus.py'
                          }
                        }
                      }
                    }
                  }
                }
              }
            }
          }
          post {
            always {
              junit 'test_integration_result.xml'
              junit 'test_www_result.xml'
              junit 'test_prometheus_result.xml'
            }
          }
        }
        stage('Performance Test') {
          steps {
            script {
              withDockerNetwork{ n ->
                docker.image("mariadb:latest").withRun("-p ${DB_PORT}:${DB_PORT} --network ${n} --network-alias db --hostname db -e MARIADB_PASSWORD='password' \
                    -e MARIADB_USER='user' -e MARIADB_DATABASE='python_rest_api' -e MARIADB_ROOT_PASSWORD=password \
                    --mount type=bind,source=${WORKSPACE}/app/init.sql,target=/docker-entrypoint-initdb.d/init.sql","--port ${DB_PORT}") { c ->
                  docker.image("redis:alpine").withRun("-p ${REDIS_PORT}:${REDIS_PORT} --network ${n} --network-alias redis --hostname redis","redis-server --port ${REDIS_PORT}") {
                    docker.image("${IMAGE}:${BUILD_NUMBER}").withRun("-p ${API_PORT}:${API_PORT} --network ${n} --network-alias api --hostname api -e DB_PASS=password \
                        -e DB_USER=user -e DB_HOST=db -e DB_PORT=${DB_PORT} -e DB_DATABASE=${DB_DATABASE} -e REDIS_HOST=redis -e REDIS_PORT=${REDIS_PORT} \
                        -e API_PORT=${API_PORT}") {
                      docker.image("blazemeter/taurus:latest").inside("--network ${n} --network-alias perf --hostname perf -u 0:0 --entrypoint='' -e API_URL=${API_URL}"){ cc ->
                        sh 'bzt -o settings.env.API_URL=${API_URL} tests/performance/bzt.yml ; ls -ltr /var/lib/jenkins/workspace/python-rest-api'
                      }
                    }
                  }
                }
              }
            }
          }
          post {
            always {
        perfReport 'bzt-result.xml'
            }
          }
        }

        stage('Security Test') {
        // requires Jenkins plugin https://plugins.jenkins.io/warnings-ng/
          steps {
            script {
              trivyImage = docker.image('aquasec/trivy:latest')
              trivyImage.inside("--entrypoint= -u root --mount type=bind,source=/var/run/docker.sock,target=/var/run/docker.sock") {
                sh '''trivy image \
                            --format=json \
                            -o results.json \
                            ${IMAGE}:${BUILD_NUMBER}'''
              }
              recordIssues(tools: [trivy(pattern: 'results.json')])
            }
          }
        }
        stage('Publish'){
            steps{
                script {
                    docker.withRegistry( '', 'dockerhub' ) {
                        docker_image.push()
                        docker_image.push('latest')
                        docker_image_www.push()
                        docker_image_www.push('latest')
                    }
                }
            }
        }
        stage('Cleanup'){
            steps{
                sh '''
                    #!/bin/bash
                    for IMG in ${IMAGE} ${IMAGE}-pytest-integration ${IMAGE}-tests ${IMAGE}-www ; do
                      for TAG in $(docker images | grep "${IMG} " | grep -v latest | sort -rnk 2 | tail -n +7 | gawk '{ print $1":"$2 }') ; do docker rmi $TAG ; done ;
                    done
                '''
                //sh 'docker volume rm $(docker volume ls -qf "dangling=true")'
                sh 'docker images'
                cleanWs()
            }
        }
    }
    post {
      success {
        setBuildStatus("Build succeeded", "SUCCESS");
      }
      failure {
        setBuildStatus("Build failed", "FAILURE");
      }
    }

}

